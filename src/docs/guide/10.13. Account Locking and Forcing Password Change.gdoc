Spring Security supports four ways of disabling a user account. When you attempt to log in, the @UserDetailsService@ implementation creates an instance of @UserDetails@ which has these accessors:

* @isAccountNonExpired()@
* @isAccountNonLocked()@
* @isCredentialsNonExpired()@
* @isEnabled()@

and if you use the [s2-quickstart|Scripts] script to create a user domain class, it creates a class with corresponding properties to manage this state.

When one of these accessors returns @false@ (i.e. @accountExpired@, @accountLocked@, or @passwordExpired@ is @true@ or @enabled@ is @false@)  a corresponding exception is thrown:

{table}
*Accessor* | *Property* | *Exception*
@isAccountNonExpired()@ | @accountExpired@ | [AccountExpiredException|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/authentication/AccountExpiredException.html]
@isAccountNonLocked()@ | @accountLocked@ | [LockedException|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/authentication/LockedException.html]
@isCredentialsNonExpired()@ | @passwordExpired@ | [CredentialsExpiredException|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/authentication/CredentialsExpiredException.html]
@isEnabled()@ | @enabled@ | [DisabledException|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/authentication/DisabledException.html]
{table}

You can configure an exception mapping in @Config.groovy@ to associate a URL to any or all of these exceptions to determine where to redirect after a failure, e.g.

{code}
grails.plugins.springsecurity.failureHandler.exceptionMappings = [
   'org.springframework.security.authentication.LockedException':             '/user/accountLocked',
   'org.springframework.security.authentication.DisabledException':           '/user/accountDisabled',
   'org.springframework.security.authentication.AccountExpiredException':     '/user/accountExpired',
   'org.springframework.security.authentication.CredentialsExpiredException': '/user/passwordExpired'
]
{code}

Without a mapping for a particular exception, the user will be redirected to the standard login fail page (by default @/login/authfail@) like they would if they had entered a bad password, but they'll see an error message from this table:

{table}
*Property* | *Default*
errors.login.disabled | "Sorry, your account is disabled."
errors.login.expired | "Sorry, your account has expired."
errors.login.passwordExpired | "Sorry, your password has expired."
errors.login.locked | "Sorry, your account is locked."
errors.login.fail | "Sorry, we were not able to find a user with that username and password."
{table}

Any of these can be customized by setting the corresponding property in @Config.groovy@, e.g.

{code}
grails.plugins.springsecurity.errors.login.locked = "None shall pass."
{code}

You can use this functionality to manually lock a user's account or expire the password, but another option would be to automate the process. For example you could use the [Quartz plugin|http://grails.org/plugin/quartz] to periodically expire everyone's password and force them to go to a page where they update it. You could also keep track of the date when the users change their passwords and use a Quartz job to expire their passwords once the password is older than some fixed max age.

h4. User cache

If configured, Spring Security will cache @UserDetails@ instances to save trips to the database. This is managed by the @cacheUsers@ configuration property, and it defaults to @false@ in the plugin but you can enable it if you wish. In general this is a minor optimization since there will most likely be only two small queries during login; one to load the user, and one to load the authorities.

If you enable this feature, you must remove any cached instances after making a change that affects login. If you don't, even though a user's account is locked or disabled, logins will still succeed since the database will be bypassed. By removing the cached data, you force them to go to the database and retrieve the latest updates.

Here is a sample Quartz job that demonstrates how you might find and disable users with passwords that are too old:

{code}
package com.mycompany.myapp

class ExpirePasswordsJob  {

   static triggers = {
      cron name: 'myTrigger', cronExpression: '0 0 0 * * ?' // midnight daily
   }

   def userCache

   void execute() {

      def users = User.executeQuery(
            'from User u where u.passwordChangeDate <= :cutoffDate',
            [cutoffDate: new Date() - 180])

      for (user in users) {
         // flush each separately so one failure doesn't rollback all of the others
         try {
            user.passwordExpired = true
            user.save(flush: true)
            userCache.removeUserFromCache user.username
         }
         catch (e) {
            log.error "problem expiring password for user $user.username : $e.message", e
         }
      }
   }
}
{code}

